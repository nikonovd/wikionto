PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://myWikiTax.de/> 
SELECT DISTINCT ?classifiername ?topclassifier1name ?topclassifier2name ?depth
WHERE {
    ?cl :name "Computer languages" .
    ?cl :hasSubclassifier ?topclassifier1 .
    ?cl :hasSubclassifier ?topclassifier2 .
    FILTER NOT EXISTS{ ?topclassifier1 :hasSubclassifier* ?topclassifier2 . }
    FILTER NOT EXISTS{ ?topclassifier2 :hasSubclassifier* ?topclassifier1 . }
    ?classifier :depth ?depth .
    FILTER EXISTS{
        ?topclassifier1 :hasSubclassifier* ?classifier .
        ?topclassifier2 :hasSubclassifier* ?classifier .
    }
    
    ?topclassifier1 :name ?topclassifier1name .
    ?topclassifier2 :name ?topclassifier2name .
    FILTER(?topclassifier1name < ?topclassifier2name && (?topclassifier1name!="Data modeling languages"||?topclassifier2name!="Markup languages"))
    ?classifier :name ?classifiername .
    
}