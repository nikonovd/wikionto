PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://myCLOnto.de/> 
SELECT DISTINCT ?typename ?toptype1name ?toptype2name
WHERE {
    ?cl :name "Computer languages" .
    ?cl :hasSubtype ?toptype1 .
    ?cl :hasSubtype ?toptype2 .
    FILTER NOT EXISTS{ ?toptype1 :hasSubtype* ?toptype2 . }
    FILTER NOT EXISTS{ ?toptype2 :hasSubtype* ?toptype1 . }
    
    FILTER EXISTS{
        ?toptype1 :hasSubtype* ?type .
        ?toptype2 :hasSubtype* ?type .
    }
    
    ?toptype1 :name ?toptype1name .
    FILTER(?toptype1name < ?toptype2name && (?toptype1name!="Data modeling languages"||?toptype2name!="Markup languages"))
    ?toptype2 :name ?toptype2name .
    ?type :name ?typename .
}
ORDER BY ?toptype2name